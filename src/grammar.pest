WHITESPACE = _{ " " | NEWLINE }
delimiter = _{ "," }
source = _{ SOI ~ (statement ~ (delimiter ~ statement)*)? ~ &EOI }

statement = _{ bind | expression }
expression = { function | comparison }
function = { "(" ~ identify ~ ("," ~ identify)* ~ ")" ~ "=>" ~ expression }

bind = { identify ~ ":" ~ expression }

comparison = { additive ~ (equal | not_equal)* }
equal = { "=" ~  additive }
not_equal = { "!=" ~  additive }

additive = { multitive ~ (add | sub)* }
add = { "+" ~  multitive }
sub = { "-" ~  multitive }

multitive = { primary ~ (mul | div | surplus)* }
mul = { "*" ~ primary }
div = { "/" ~ primary }
surplus = { "%" ~ primary }
  
primary = _{
  "(" ~ parenthesis ~ ")" | block | _if | literal | evaluation
}

parenthesis = { expression }

block = { "{" ~ (statement ~ (delimiter ~ statement)*)? ~ "}" }
_if = { "if" ~ expression ~ expression ~ expression }
literal = _{ number | string_literal }


string_literal = _{ "\"" ~ string ~ "\"" }
string = { char* }
char = @{ (!"\"" ~ ANY) }

number = ${ ASCII_DIGIT+ }

evaluation = { identify ~ calling* ~ ("." ~ identify ~ calling*)* }
calling = { "(" ~ expression ~ ")" }
identify = ${ (ASCII_ALPHA | "_")+ }
