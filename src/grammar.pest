WHITESPACE = _{ " " | NEWLINE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

delimiter = _{ "," }
source = _{ SOI ~ (statement ~ (delimiter ~ statement)*)? ~ &EOI }

statement = _{ bind | expression }
expression = { function | _if | comparison }
function = { "(" ~ identify ~ ("," ~ identify)* ~ ")" ~ "=>" ~ expression }

bind = { (identify | string_literal) ~ ":" ~ expression }

comparison = { additive ~ (equal | not_equal)* }
equal = { "=" ~  additive }
not_equal = { "!=" ~  additive }

additive = { multitive ~ (add | sub)* }
add = { "+" ~  multitive }
sub = { "-" ~  multitive }

multitive = { primary ~ (mul | div | surplus)* }
mul = { "*" ~ primary }
div = { "/" ~ primary }
surplus = { "%" ~ primary }
  
primary = { primary_part ~ ("." ~ primary_part)* }

primary_part = { atom ~ (calling | indexing)* }

calling = { "(" ~ expression ~ ")" }
indexing = { "[" ~ expression ~ "]" }

atom = _{
  "(" ~ parenthesis ~ ")" | block | list | literal | null | identify
}

identify = ${ (ASCII_ALPHA | "_")+ }

null = { "null" }

parenthesis = { expression }

list = { "[" ~ (expression ~ (delimiter ~ expression)*)? ~ "]" }
block = { "{" ~ (statement ~ (delimiter ~ statement)*)? ~ "}" }
_if = { "if" ~ expression ~ expression ~ expression }
literal = _{ number | string_literal }

string_literal = ${ "\"" ~ string  ~ "\"" }
string = { (!"\"" ~ ("\\\"" | ANY | WHITESPACE))* }

number = ${ ASCII_DIGIT+ }
